# Maintainer: Gao xiang<hughgao01@gmail.com>
# Modified by: Eric Berquist <eric DOT berquist AT gmail DOT com>

pkgname=gamess
pkgver=2017R1
pkgrel=2
pkgdesc='A general ab initio quantum chemistry package. For single machine use only.'
arch=('x86_64')
url='http://www.msg.ameslab.gov/GAMESS/GAMESS.html'
license=('custom')
depends=('gcc-fortran' 'tcsh' 'openblas-lapack')
install=${pkgname}.install
# You MUST download the package from http://www.msg.ameslab.gov/GAMESS/GAMESS.html and put it in the PKGBUILD folder!
source=('gamess-current.tar.gz::file://gamess-current.tar.gz'
        'rungms.patch'
        'openblas.patch')
md5sums=('e9fa2e725ccbd69f4d996ab68bb65ff2'
         '0bee0a494e6e71737209dc21c265e815'
         '24e81ddc1e6be9f2c0a2d71fdf2236e2')

prepare() {
    cd "${pkgname}"

    _GMS_PATH="$(pwd)"
    _GMS_BUILD_DIR="$(pwd)"
    mkdir -p "${_GMS_BUILD_DIR}"/{object,tools,ddi,tests}

    # generate install.info
    cat > "${_GMS_BUILD_DIR}"/install.info <<EOF
# install path
setenv GMS_PATH            $_GMS_PATH
# build path
setenv GMS_BUILD_DIR       $_GMS_BUILD_DIR
# machine type
setenv GMS_TARGET          linux64
# FORTRAN compiler setup
setenv GMS_FORTRAN         gfortran
# gamess does not support gfortran 6.x, using 5.9 instead
setenv GMS_GFORTRAN_VERNO  5.3
# mathematical library setup
setenv GMS_MATHLIB         openblas
setenv GMS_MATHLIB_PATH    /usr/lib
# parallel message passing model setup
setenv GMS_DDI_COMM        sockets
# LIBCCHEM CPU/GPU code interface
setenv GMS_LIBCCHEM        false
# These only matter when attempting to use LIBCCHEM.
# Needs 'cuda' and 'hdf5-cpp-fortran' with static libraries.
# setenv GMS_CUDA_PATH       /opt/cuda
# tesla (1.3), fermi (2.0), or kepler (3.5)
# setenv GMS_CUDA_BOARD      fermi
# setenv GMS_HDF5_PATH       /usr
# Intel Xeon Phi build: true/false
setenv GMS_PHI             false
# Shared memory type: sysv/posix
setenv GMS_SHMTYPE         sysv
# OpenMP support: true/false
setenv GMS_OPENMP          false
EOF

    # test the compiler setup by creating actvte.x
    sed -e "s/^\*UNX/    /" tools/actvte.code > actvte.f
    gfortran -o "${_GMS_BUILD_DIR}"/tools/actvte.x actvte.f
    rm actvte.f

    # patch the rungms driver script
    patch -p1 -i "${srcdir}"/rungms.patch
    # patch for using OpenBLAS as math library
    patch -p1 -i "${srcdir}"/openblas.patch
}

build() {
    cd "${pkgname}"/ddi
    ./compddi
    mv ddikick.x ..

    cd ..
    ./compall

    # cd ./libcchem
    # ./build-cchem

    # cd ..
    ./lked gamess 01
}

package() {
    cd "${pkgname}"

    install -d "${pkgdir}"/opt/gamess "${pkgdir}"/usr/bin
    cp -a ./* "${pkgdir}"/opt/gamess/
    install -Dm755 "${pkgdir}"/opt/gamess/rungms "${pkgdir}"/usr/bin
}
