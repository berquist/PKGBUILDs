# Maintainer: eolianoe <eolianoe [at] gmail [DoT] com>
# Contributor: Jiaxi Hu <sftrytry _AT_ gmail _DOT_ com>
# Contributor: Giuseppe Borzi <gborzi _AT_ ieee _DOT_ org>
# Modified by: Eric Berquist <eric DOT berquist AT gmail DOT com>

pkgname=openblas
_PkgName=OpenBLAS
pkgver=0.2.20
_blasver=3.7.0
pkgrel=1
pkgdesc="Optimized BLAS library based on GotoBLAS2 1.13 BSD"
arch=("x86_64")
url="http://xianyi.github.com/OpenBLAS/"
license=("BSD")
depends=("gcc-libs")
makedepends=("perl" "gcc-fortran")
provides=("openblas" "blas=${_blasver}")
conflicts=("openblas" "blas")
options=(!strip !makeflags !buildflags !emptydirs staticlibs)
source=(${_PkgName}-${pkgver}.tar.gz::https://github.com/xianyi/${_PkgName}/archive/v${pkgver}.tar.gz)
sha256sums=('5ef38b15d9c652985774869efd548b8e3e972e1e99475c673b25537ed7bcf394')

NTHREADS=$(\grep "cores" /proc/cpuinfo | tail -n 1 | sed 's/cpu cores.*: //')
_config="FC=gfortran USE_OPENMP=1 NO_CBLAS=1 NO_LAPACK=1 NUM_THREADS=${NTHREADS} NO_AFFINITY=1"

build() {
  cd "${srcdir}/${_PkgName}-${pkgver}"

  make ${_config} libs netlib shared
}

check() {
  cd "${srcdir}/${_PkgName}-${pkgver}"

  make ${_config} tests
}

package() {
  cd "${srcdir}/${_PkgName}-${pkgver}"

  make ${_config} PREFIX=/usr DESTDIR="${pkgdir}" install

  # Install license
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  # Symlink to "provide" libraries that are all contained in OpenBLAS.
  cd "${pkgdir}/usr/lib/"
  targets=(
    blas
  )

  ln -sfv libopenblas.so libopenblas.so.0
  for target in ${targets[@]}; do
    ln -sfv libopenblas.so lib${target}.so
    ln -sfv libopenblas.so lib${target}.so.0
    ln -sfv libopenblas.so lib${target}.so.${_blasver:0:1}
    ln -sfv libopenblas.so lib${target}.so.${_blasver}
    ln -sfv libopenblas.a  lib${target}.a
  done

  sed -i -e "s%$pkgdir%%" "$pkgdir/usr/lib/cmake/openblas/OpenBLASConfig.cmake"
  sed -i -e "s%$pkgdir%%" "$pkgdir/usr/lib/pkgconfig/openblas.pc"
}
