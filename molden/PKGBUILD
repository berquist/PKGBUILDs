# Maintainer: Dan Maftei <dan.maftei@gmail.com>
# Modified by: Eric Berquist <eric DOT berquist AT gmail DOT com>

pkgname="molden"
pkgver=6.5
pkgrel=2
pkgdesc="A program for molecular and electronic structure visualization"
arch=('i686' 'x86_64')
url="http://www.cmbi.ru.nl/molden/"
license=('custom')
depends=('gcc-libs' 'glu' 'libx11' 'libxmu')
makedepends=('gcc-fortran' 'xorgproto' 'vi')
optdepends=(
   'openbabel: to create 2D images of the molecules in a .sdf file'
   'wget: to fetch PDB from rcsb.org'
)
provides=('molden')
source=(
    "ftp://ftp.cmbi.umcn.nl/pub/molgraph/molden/$pkgname$pkgver.tar.gz"
)
md5sums=('97bfffd55e166ca3a9fbddee111993ff')

prepare() {
  cd "molden$pkgver"
  # Patch Makefile for surf utility to reflect
  # the replacement of missing makedepend
  sed -i 's/@.*makedepend.*$/@ \$(CC) \$(INCLUDE) -M \$(SRCS) \> makedep/' src/surf/Makefile

  # Patch to compile with gfortran 10
  # Contributed by Panadestein on 5/31/2020
  sed -i 's/FFLAGS = -g ${AFLAG}/& -fallow-argument-mismatch/g' makefile
  sed -i 's/FFLAGS = -c -g -ffast-math -funroll-loops -O3/& -fallow-argument-mismatch/g' src/ambfor/makefile
}

build() {
  cd "molden$pkgver"
  make
}

package() {
  cd "molden$pkgver"
  install -t "$pkgdir/usr/bin/"  -Dm755 bin/{molden,gmolden}
  install -t "$pkgdir/usr/lib/$pkgname/" -Dm755 bin/{ambfor,ambmd,surf}
  install -t "$pkgdir/usr/share/doc/$pkgname" -Dm755 doc/figures.ps.Z  doc/manual.ps.Z doc/manual.txt.Z
  install -t "$pkgdir/usr/share/licenses/$pkgname/" -Dm755 CopyRight COMMERCIAL_LICENSE REGISTER
}
