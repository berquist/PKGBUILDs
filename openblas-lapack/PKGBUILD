# Maintainer: eolianoe <eolianoe [at] gmail [DoT] com>
# Contributor: Jiaxi Hu <sftrytry _AT_ gmail _DOT_ com>
# Contributor: Giuseppe Borzi <gborzi _AT_ ieee _DOT_ org>
# Modified by: Eric Berquist <eric DOT berquist AT gmail DOT com>

pkgname=openblas-lapack
_PkgName=OpenBLAS
_pkgname=openblas
pkgver=0.3.0
_lapackver=3.8.0
pkgrel=1
pkgdesc="Optimized BLAS library based on GotoBLAS2 1.13 BSD (also providing LAPACK)"
arch=("x86_64")
url="http://xianyi.github.com/OpenBLAS/"
license=("BSD")
depends=("gcc-libs")
makedepends=("perl" "gcc-fortran")
provides=("openblas" "blas=${_lapackver}" "lapack=${_lapackver}" "cblas=${_lapackver}" "lapacke=${_lapackver}")
conflicts=("openblas" "blas" "lapack" "cblas" "lapacke" "atlas-lapack-base")
options=(!strip !makeflags !buildflags !emptydirs staticlibs)
source=(${_PkgName}-${pkgver}.tar.gz::https://github.com/xianyi/${_PkgName}/archive/v${pkgver}.tar.gz)
sha256sums=('cf51543709abe364d8ecfb5c09a2b533d2b725ea1a66f203509b21a8e9d8f1a1')

NTHREADS=$(\grep "cores" /proc/cpuinfo | tail -n 1 | sed 's/cpu cores.*: //')
_config="FC=gfortran USE_OPENMP=1 NO_LAPACK=0 NUM_THREADS=${NTHREADS} NO_AFFINITY=1 BUILD_LAPACK_DEPRECATED=1 BUILD_RELAPACK=1"

build() {
  cd "${srcdir}/${_PkgName}-${pkgver}"

  make ${_config} libs netlib shared
}

check() {
  cd "${srcdir}/${_PkgName}-${pkgver}"

  make ${_config} tests
}

package() {
  cd "${srcdir}/${_PkgName}-${pkgver}"

  make ${_config} PREFIX=/usr DESTDIR="${pkgdir}" install

  # Install license
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  # Symlink to "provide" libraries that are all contained in OpenBLAS.
  cd "${pkgdir}/usr/lib/"
  targets=(
    blas
    f77blas
    cblas
    lapack
    lapacke
  )

  ln -sfv libopenblas.so libopenblas.so.${_lapackver:0:1}
  for target in ${targets[@]}; do
    ln -sfv libopenblas.so lib${target}.so
    ln -sfv libopenblas.so lib${target}.so.${_lapackver:0:1}
    ln -sfv libopenblas.so lib${target}.so.${_lapackver}
    ln -sfv libopenblas.a  lib${target}.a
  done

  sed -i -e "s%$pkgdir%%" "$pkgdir/usr/lib/cmake/openblas/OpenBLASConfig.cmake"
  sed -i -e "s%$pkgdir%%" "$pkgdir/usr/lib/pkgconfig/openblas.pc"
}
