# Maintainer: eolianoe <eolianoe [at] gmail [DoT] com>
# Contributor: Jiaxi Hu <sftrytry _AT_ gmail _DOT_ com>
# Contributor: Giuseppe Borzi <gborzi _AT_ ieee _DOT_ org>
# Modified by: Eric Berquist <eric DOT berquist AT gmail DOT com>

pkgname=openblas-lapack
_PkgName=OpenBLAS
_pkgname=openblas
pkgver=0.2.20
_lapackver=3.7.0
pkgrel=1
pkgdesc="Optimized BLAS library based on GotoBLAS2 1.13 BSD (also providing LAPACK)"
arch=("i686" "x86_64")
url="http://xianyi.github.com/OpenBLAS/"
license=("BSD")
depends=("gcc-libs")
makedepends=("perl" "gcc-fortran")
provides=("openblas" "blas=${_lapackver}" "lapack=${_lapackver}" "cblas=${_lapackver}" "lapacke=${_lapackver}")
conflicts=("openblas" "blas" "lapack" "cblas" "lapacke" "atlas-lapack-base")
options=(!makeflags !buildflags !emptydirs staticlibs)
source=(${_PkgName}-${pkgver}.tar.gz::https://github.com/xianyi/${_PkgName}/archive/v${pkgver}.tar.gz)
sha256sums=('5ef38b15d9c652985774869efd548b8e3e972e1e99475c673b25537ed7bcf394')

NTHREADS=$(\grep "cores" /proc/cpuinfo | tail -n 1 | sed 's/cpu cores.*: //')
_config="FC=gfortran USE_OPENMP=1 USE_THREAD=1 NO_LAPACK=0 NUM_THREADS=${NTHREADS} BUILD_LAPACK_DEPRECATED=1 BUILD_RELAPACK=1"

build() {
  cd "${srcdir}/${_PkgName}-${pkgver}"

  make ${_config} libs netlib shared
}

check() {
  cd "${srcdir}/${_PkgName}-${pkgver}"

  make ${_config} tests
}

package() {
  cd "${srcdir}/${_PkgName}-${pkgver}"

  make ${_config} PREFIX=/usr DESTDIR="${pkgdir}" install

  # Install license
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  # Symlink to "provide" libraries that are all contained in OpenBLAS.
  cd "${pkgdir}/usr/lib/"
  targets=(
    blas
    f77blas
    cblas
    lapack
    lapacke
  )

  ln -sv libopenblas.so libopenblas.so.${_lapackver:0:1}
  for target in ${targets[@]}; do
    ln -sf libopenblas.so lib${target}.so
    ln -sf libopenblas.so lib${target}.so.${_lapackver:0:1}
    ln -sf libopenblas.so lib${target}.so.${_lapackver}
    ln -sf libopenblas.a  lib${target}.a
  done
}
