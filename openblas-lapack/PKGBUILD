# Maintainer: eolianoe <eolianoe [at] gmail [DoT] com>
# Contributor: Jiaxi Hu <sftrytry _AT_ gmail _DOT_ com>
# Contributor: Giuseppe Borzi <gborzi _AT_ ieee _DOT_ org>
# Modified by: Eric Berquist <eric DOT berquist AT gmail DOT com>

pkgname=openblas-lapack
_pkgname=OpenBLAS
pkgver=0.2.15
pkgrel=2
pkgdesc="Optimized BLAS library based on GotoBLAS2 1.13 BSD (providing BLAS, LAPACK, and CBLAS)"
arch=("i686" "x86_64")
url="http://xianyi.github.com/OpenBLAS/"
license=("BSD")
depends=("gcc-libs")
makedepends=("perl" "gcc-fortran")
_majorver=3
_fullver=3.5.0
provides=("openblas" "blas=${_fullver}" "lapack=${_fullver}" "cblas=${_fullver}")
conflicts=("openblas" "blas" "lapack" "cblas")
options=(!emptydirs staticlibs)
source=(${_pkgname}-${pkgver}.tar.gz::https://github.com/xianyi/OpenBLAS/archive/v${pkgver}.tar.gz)
sha256sums=('73c40ace5978282224e5e122a41c8388c5a19e65a6f2329c2b7c0b61bacc9044')

NTHREADS=$(\grep "cores" /proc/cpuinfo | tail -n 1 | sed 's/cpu cores.*: //')
_config="FC=gfortran USE_OPENMP=1 USE_THREAD=1 MAJOR_VERSION=${_majorver} NO_LAPACK=0 NUM_THREADS=${NTHREADS}"

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  make ${_config} libs netlib shared
}

check() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  make ${_config} tests
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  make ${_config} PREFIX=/usr DESTDIR="${pkgdir}" install

  # Install license
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  # Symlink to provide BLAS, CBLAS and LAPACK
  cd "${pkgdir}/usr/lib/"
  # BLAS
  ln -sf libopenblas.so libblas.so
  ln -sf libopenblas.so libblas.so.${_majorver}
  ln -sf libopenblas.so libblas.so.${_fullver}
  ln -sf libopenblas.a  libblas.a
  # CBLAS (C interface)
  ln -sf libopenblas.so libcblas.so
  ln -sf libopenblas.so libcblas.so.${_majorver}
  ln -sf libopenblas.so libcblas.so.${_fullver}
  ln -sf libopenblas.a  libcblas.a
  # LAPACK
  ln -sf libopenblas.so liblapack.so
  ln -sf libopenblas.so liblapack.so.${_majorver}
  ln -sf libopenblas.so liblapack.so.${_fullver}
  ln -sf libopenblas.a  liblapack.a
  # LAPACKE (C interface)
  ln -sf libopenblas.so liblapacke.so
  ln -sf libopenblas.so liblapacke.so.${_majorver}
  ln -sf libopenblas.so liblapacke.so.${_fullver}
  ln -sf libopenblas.a  liblapacke.a
}
