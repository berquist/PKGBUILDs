# Maintainer: thrasibule <guillaume.horel@gmail.com>
# Contributor: eolianoe <eolianoe [at] gmail [DoT] com>
# Contributor: Jiaxi Hu <sftrytry _AT_ gmail _DOT_ com>
# Contributor: Giuseppe Borzi <gborzi _AT_ ieee _DOT_ org>
# Modified by: Eric Berquist <eric DOT berquist AT gmail DOT com>

pkgname=openblas-lapack
_PkgName=OpenBLAS
_pkgname=openblas
pkgver=0.3.12
_lapackver=3.9.0
_blasver=3.8.0
pkgrel=1
pkgdesc="Optimized BLAS library based on GotoBLAS2 1.13 BSD (also providing LAPACK)"
arch=("x86_64")
url="http://xianyi.github.com/OpenBLAS/"
license=("BSD")
depends=("gcc-libs")
makedepends=("perl" "gcc-fortran")
provides=("openblas" "blas=${_blasver}" "lapack=${_lapackver}" "cblas=${_blasver}" "lapacke=${_lapackver}")
conflicts=("openblas" "blas" "lapack" "cblas" "lapacke" "atlas-lapack-base")
options=(!strip !makeflags !buildflags !emptydirs staticlibs)
source=(${_PkgName}-${pkgver}.tar.gz::https://github.com/xianyi/${_PkgName}/archive/v${pkgver}.tar.gz)
sha256sums=('65a7d3a4010a4e3bd5c0baa41a234797cd3a1735449a4a5902129152601dc57b')

NTHREADS=$(\grep "cores" /proc/cpuinfo | tail -n 1 | sed 's/cpu cores.*: //')
_config="FC=gfortran USE_OPENMP=1 USE_THREAD=1 NO_LAPACK=0 NUM_THREADS=${NTHREADS} NO_AFFINITY=1 BUILD_LAPACK_DEPRECATED=1 BUILD_RELAPACK=1 TARGET=ZEN"

build() {
  cd "${srcdir}/${_PkgName}-${pkgver}"

  make ${_config} CFLAGS="${CFLAGS}" libs netlib shared
}

check() {
  cd "${srcdir}/${_PkgName}-${pkgver}"

  make ${_config} tests
}

package() {
  cd "${srcdir}/${_PkgName}-${pkgver}"

  make ${_config} PREFIX=/usr DESTDIR="${pkgdir}" install

  # Install license
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  # Symlink to "provide" libraries that are all contained in OpenBLAS.
  cd "${pkgdir}/usr/lib/"
  declare -A targets=( [blas]=${_blasver} [f77blas]=${_blasver} [cblas]=${_blasver} [lapack]=${_lapackver} [lapacke]=${_lapackver} )

  sed -i -e "s%$pkgdir%%" "${pkgdir}"/usr/lib/cmake/openblas/OpenBLASConfig.cmake
  sed -i -e "s%$pkgdir%%" "${pkgdir}"/usr/lib/pkgconfig/openblas.pc
  for target in blas cblas lapack; do
    cp -a "${pkgdir}"/usr/lib/pkgconfig/{openblas,$target}.pc
  done

  ln -sfv libopenblas.so libopenblas.so.${_lapackver:0:1}
  for target in "${!targets[@]}"; do
    ver=${targets[${target}]}
    ln -sfv libopenblas.so lib${target}.so
    ln -sfv libopenblas.so lib${target}.so.${ver:0:1}
    ln -sfv libopenblas.so lib${target}.so.${ver}
    ln -sfv libopenblas.a  lib${target}.a
  done
}
