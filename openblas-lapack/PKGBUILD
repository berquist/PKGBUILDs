# Maintainer: eolianoe <eolianoe [at] gmail [DoT] com>
# Contributor: Jiaxi Hu <sftrytry _AT_ gmail _DOT_ com>
# Contributor: Giuseppe Borzi <gborzi _AT_ ieee _DOT_ org>
# Modified by: Eric Berquist <eric DOT berquist AT gmail DOT com>

pkgname=openblas-lapack
_pkgname=OpenBLAS
pkgver=0.2.17
pkgrel=2
pkgdesc="Optimized BLAS library based on GotoBLAS2 1.13 BSD (also providing LAPACK)"
arch=("i686" "x86_64")
url="http://xianyi.github.com/OpenBLAS/"
license=("BSD")
depends=("gcc-libs")
makedepends=("perl" "gcc-fortran")
_majorver=3
_fullver=3.6.0
provides=("openblas" "blas=${_fullver}" "lapack=${_fullver}" "cblas=${_fullver}" "lapacke=${_fullver}")
conflicts=("openblas" "blas" "lapack" "cblas" "lapacke" "atlas-lapack-base")
options=(!emptydirs staticlibs)
source=(${_pkgname}-${pkgver}.tar.gz::https://github.com/xianyi/OpenBLAS/archive/v${pkgver}.tar.gz)
sha256sums=('0fe836dfee219ff4cadcc3567fb2223d9e0da5f60c7382711fb9e2c35ecf0dbf')

NTHREADS=$(\grep "cores" /proc/cpuinfo | tail -n 1 | sed 's/cpu cores.*: //')
_config="FC=gfortran USE_OPENMP=1 USE_THREAD=1 MAJOR_VERSION=${_majorver} NO_LAPACK=0 NUM_THREADS=${NTHREADS} BUILD_LAPACK_DEPRECATED=1"

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  make ${_config} libs netlib shared
}

check() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  make ${_config} tests
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  make ${_config} PREFIX=/usr DESTDIR="${pkgdir}" install

  # Install license
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  # Symlink to "provide" libraries that are all contained in OpenBLAS.
  cd "${pkgdir}/usr/lib/"
  targets=(
    blas
    f77blas
    cblas
    lapack
    lapacke
  )
  for target in ${targets[@]}; do
    ln -sf libopenblas.so lib${target}.so
    ln -sf libopenblas.so lib${target}.so.${_majorver}
    ln -sf libopenblas.so lib${target}.so.${_fullver}
    ln -sf libopenblas.a  lib${target}.a
  done
}
